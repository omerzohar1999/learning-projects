// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Not16(in=x, out=notx);
    Not16(in=y, out=noty);
    Inc16(in=notx, out=minusx);
    Inc16(in=noty, out=minusy);
    Add16(a=minusx, b=x, out=zero);
    Mux16(a=x, b=zero, sel=zx,out=xBeforeNegation); // address zx
    Not16(in=xBeforeNegation, out=xNegated);
    Mux16(a=xBeforeNegation, b=xNegated, sel=nx, out=xInput); // address nx
    Mux16(a=y, b=zero, sel=zy, out=yBeforeNegation); // address zy
    Not16(in=yBeforeNegation, out=yNegated);
    Mux16(a=yBeforeNegation, b=yNegated, sel=ny, out=yInput); // address ny
    And16(a=xInput, b=yInput, out=AndOutput); // if f == 0
    Add16(a=xInput, b=yInput, out=AddOutput); // if f == 1
    Mux16(a=AndOutput, b=AddOutput, sel=f, out=output0); // choose according to f
    Not16(in=output0, out=notoutput);
    Mux16(a=output0, b=notoutput, sel=no, out=output1); // choose output or not output according to no
    ISNEG(in=output1, out=ng);
    ISZ(in=output1, out=zr);
    And16(a=output1, b=output1, out=out);
}